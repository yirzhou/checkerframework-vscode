plugins {
    id 'java'
    id 'application'
}

group 'org.checkerframework'
version '0.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    ext.checkerFrameworkVersion = '2.5.1'
    implementation "org.checkerframework:javacutil:${checkerFrameworkVersion}"
    implementation group: "org.eclipse.lsp4j", name: "org.eclipse.lsp4j", version: "0.4.1"
    implementation 'com.google.code.gson:gson:2.8.4'
}

mainClassName = 'org.checkerframework.LanguageServerStarter'

task fatJar(group: 'distribution', type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'CheckerFramework LSP',
                'Implementation-Version': version,
                'Main-Class': 'org.checkerframework.LanguageServerStarter'
    }
    baseName = project.name
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
    with jar
}

task distribute(group: 'Distribution', dependsOn: 'installDist') {
    description 'Build, install, and copy distribution to dist directory.'
    doFirst {
        installDist
    }
    doLast {
        copy {
            from "${buildDir}/install/"
            into "${projectDir}/dist"
        }
    }
}

clean.doFirst {
    delete "${projectDir}/dist"
}